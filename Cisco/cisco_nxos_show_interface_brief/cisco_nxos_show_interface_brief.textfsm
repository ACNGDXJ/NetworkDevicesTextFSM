Value Required INTERFACE_NAME ([\w+/]+)
Value VRF (\S+)
Value INTERFACE_PHYSICAL_STATE (\S+)
Value INTERFACE_PROTOCOL_STATE (up|down)
Value IP (\d+\.\d+\.\d+\.\d+)
Value SPEED (\S+)
Value MTU (\d+)
Value VLAN ([\d+-]+)
Value TYPE (\S+)
Value MODE (routed|access|trunk|pvlan|fabric)
Value REASON (\S+((\s\w+)+)?)
Value PORTCH (\S+)
Value DESCRIPTION (\S+((\s\w+)+)?)
Value PROTOCOL (\S+)

Start
  ^Port\s+VRF\s+Status\s+IP\s+Address\s+Speed\s+MTU -> Management 
  ^Ethernet\s+VLAN\s+Type\sMode\s+Status\s+Reason\s+Speed\s+Port\s*$$ -> Ethernet
  ^Interface\s+Ch\s+ -> Ethernet 
  ^Interface\s+Status\s+Description\s*$$ -> Loopback
  ^Interface\s+Secondary\s+VLAN\(Type\)\s+Status\s+Reason -> VLAN
  ^Port-channel\s+VLAN\s+Type\sMode\s+Status\s+Reason\s+Speed\s+Protocol -> PORTCHANNEL
  ^---+$$
  ^\S+\s#
  ^. -> Error Start

Management
  ^${INTERFACE_NAME}\s+${VRF}\s+${INTERFACE_PROTOCOL_STATE}\s+${IP}\s+${SPEED}\s+${MTU} -> Record
  ^Ethernet\s+VLAN\s+Type\s+Mode\s+Status\s+Reason\s+Speed\s+Port -> Start
  ^---+$$
  ^\s*$$ -> Start
  ^. -> Error Management

Ethernet
  ^Interface\s+Ch\s+#\s*$$
  ^${INTERFACE_NAME}\s+${VLAN}\s+${TYPE}\s+${MODE}\s+${INTERFACE_PROTOCOL_STATE}\s+${REASON}\s+${SPEED}\s+${PORTCH} -> Record
  ^---+$$
  ^\s*$$ -> Start
  ^. -> Error Etherenet

Loopback
  ^${INTERFACE_NAME}\s+${INTERFACE_PROTOCOL_STATE}\s+${DESCRIPTION} -> Record
  ^---+$$
  ^\s*$$ -> Start
  ^. -> Error Loopback

VLAN
  ^${INTERFACE_NAME}\s+${TYPE}\s+${INTERFACE_PROTOCOL_STATE}\s+${REASON} -> Record
  ^---+$$
  ^\s*$$ -> Start
  ^. -> Error VLAN

PORTCHANNEL
  ^Interface\s*$$
  ^${INTERFACE_NAME}\s+${VLAN}\s+${TYPE}\s+${MODE}\s+${INTERFACE_PROTOCOL_STATE}\s+${REASON}\s+${SPEED}\s+${PROTOCOL} -> Record
  ^---+$$
  ^\s*$$ -> Start
  ^. -> Error PORTCHANNEL
